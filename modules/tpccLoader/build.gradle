plugins {
    id 'tsubakuro-examples.app-conventions'
}

dependencies {
    implementation 'commons-cli:commons-cli:1.4'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

tasks.register('runTpccLoader', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.tsurugidb.tsubakuro.examples.tpccLoader.Main'

    systemProperty 'com.tsurugidb.tsubakuro.jnilib', "${buildDir}/native/libtsubakuro.so"
    systemProperty 'tsurugi.dbname', findProperty('tsurugi.dbname') ?: 'ipc:tsurugi'

    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', findProperty('loglevel') ?: 'info'
    systemProperty 'org.slf4j.simpleLogger.showThreadName', 'false'
    systemProperty 'org.slf4j.simpleLogger.showShortLogName', 'true'
    systemProperty 'org.slf4j.simpleLogger.levelInBrackets', 'true'

    dependsOn copyNativeLib
}

startScripts {
    applicationName = 'tpccLoader'
    mainClass = 'com.tsurugidb.tsubakuro.examples.tpccLoader.Main'
    applicationDefaultJvmArgs = [
        '-Dcom.tsurugidb.tsubakuro.jniverify=false',
        '-Dtsurugi.dbname=ipc:tsurugi'
    ]
}

distTar {
    archiveFileName = "${project.name}.tar"
}
