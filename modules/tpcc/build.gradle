plugins {
    id 'tsubakuro-examples.app-conventions'
}

tasks.register('runTpcc', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.tsurugidb.tsubakuro.examples.tpcc.Main'

    systemProperty 'com.tsurugidb.tsubakuro.jnilib', "${buildDir}/native/libtsubakuro.so"
    systemProperty 'com.tsurugidb.tsubakuro.jniverify', 'false'
    systemProperty 'tsurugi.dbname', findProperty('tsurugi.dbname') ?: 'ipc:tsurugi'

    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', findProperty('loglevel') ?: 'info'
    systemProperty 'org.slf4j.simpleLogger.showThreadName', 'false'
    systemProperty 'org.slf4j.simpleLogger.showShortLogName', 'true'
    systemProperty 'org.slf4j.simpleLogger.levelInBrackets', 'true'

    dependsOn copyNativeLib
}

startScripts {
    applicationName = 'tpcc'
    mainClass = 'com.tsurugidb.tsubakuro.examples.tpcc.Main'
    applicationDefaultJvmArgs = [
        '-Dcom.tsurugidb.tsubakuro.jniverify=false',
        '-Dtsurugi.dbname=ipc:tsurugi'
    ]
}

distTar {
    archiveFileName = "${project.name}.tar.gz"
    archiveExtension = 'tar.gz'
    compression = Compression.GZIP
}
