plugins {
    id 'tsubakuro-examples.java-conventions'
}

configurations {
    nativeLib
}

dependencies {
    implementation "com.tsurugidb.tsubakuro:tsubakuro-session:${tsubakuroVersion}"
    implementation "com.tsurugidb.tsubakuro:tsubakuro-connector:${tsubakuroVersion}"

    nativeLib "com.tsurugidb.tsubakuro:tsubakuro-jni:${tsubakuroVersion}"
}

tasks.named('test') {
    systemProperty 'com.tsurugidb.tsubakuro.jnilib', "${buildDir}/native/libtsubakuro.so"
    systemProperty 'tsurugi.dbname', findProperty('tsurugi.dbname') ?: 'ipc:tateyama'

    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', findProperty('loglevel') ?: 'info'
    systemProperty 'org.slf4j.simpleLogger.showThreadName', 'false'
    systemProperty 'org.slf4j.simpleLogger.showShortLogName', 'true'
    systemProperty 'org.slf4j.simpleLogger.levelInBrackets', 'true'

    systemProperty 'tsurugi.tsubakuro-examples.postgresDir', findProperty('tsurugi.tsubakuro-examples.postgresDir') ?: "${rootDir}/third_party/postgres"

    dependsOn copyNativeLib

    jvmArgs '--add-opens=java.base/java.nio=ALL-UNNAMED'
}

task copyNativeLib(type: Copy) {
    from  configurations.nativeLib
    into "${buildDir}/native"
    rename "tsubakuro-jni-${tsubakuroVersion}.so", 'libtsubakuro.so'
}
